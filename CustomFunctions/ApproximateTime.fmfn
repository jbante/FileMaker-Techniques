/*******************************************************************************
 * ApproximateTime ( theTime )
 *
 * @param {time} theTime: a time
 *
 * @return {text} theTime expressed as an interval length in only its most
 * significant order of magnitude (hours, minutes, or seconds)
 *
 * @history 2011-06-05 Created by jbante
 ******************************************************************************/

Let ( [
	_seconds = Round ( GetAsNumber ( GetAsTime ( theTime ) ) ; 0 );
	_minutes = _seconds / 60
];
	Case (
		_minutes > 120;	// Hours timescale
			"~"
			& Round ( _minutes / 60 ; 0 )
			& " hours";

		_minutes > 60;	// Hours timescale
			"~"
			& Round ( _minutes / 60 ; 1 )
			& " hours";

		_seconds > 120;	// Minutes timescale
			"~"
			& Round ( _seconds / 60 ; 0 )
			& " minutes";

		_seconds > 60;	// Minutes timescale
			"~"
			& Round ( _seconds / 60 ; 1 )
			& " minutes";

		/* Else, Seconds timescale */
			"~"
			& _seconds
			& " seconds"
	)
)